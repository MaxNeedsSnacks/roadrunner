//file:noinspection all

plugins {
    id 'dev.architectury.loom' version '0.7.2-SNAPSHOT'
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id 'maven-publish'
}

loom {
    silentMojangMappingsLicense()

    mixinConfigs = [
            "roadrunner.mixins.json"
    ]
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = project.maven_group
version = project.mod_version
archivesBaseName = "${project.name}-mc${project.minecraft_version}"

def ENV = System.getenv()

def build_release = ENV.BUILD_RELEASE == "true"
def build_id = ENV.BUILD_ID

if (!build_release) {
    version += "-SNAPSHOT"
}

if (build_id != null) {
    version += "+build.${build_id}"
}

// TODO: fix issue with multi source sets (archloom issue)
/*sourceSets {
    api {
        java {
            compileClasspath += main.compileClasspath
        }
    }

    main {
        java {
            compileClasspath += api.output
            runtimeClasspath += api.output
        }
    }
}*/

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"
}


processResources {
    // define properties that can be used during resource processing
    inputs.property "version", project.version

    // this will replace the property "${version}" in your mods.toml
    // with the version you've defined in your gradle.properties
    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

    // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
    // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
    // We'll use that if it's available, but otherwise we'll use the older option.
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }

    // add some additional metadata to the jar manifest
    manifest {
        attributes([
                "Specification-Title"     : "roadrunner",
                "Specification-Vendor"    : "MaxNeedsSnacks",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : "MaxNeedsSnacks",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// Include the license in all JAR outputs
tasks.withType(Jar) {
    from "LICENSE.txt"
    //from sourceSets.api.output
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = "roadrunner-forge"
            artifact(remapJar) {
                classifier null
            }
        }
    }

    repositories {
        if (ENV.SAPS_TOKEN) {
            maven {
                url "https://maven.saps.dev/minecraft"
                credentials {
                    username = "max"
                    password = "${ENV.SAPS_TOKEN}"
                }
            }
        }
    }
}

/*
task apiJar(type: Jar) {
    classifier "api-dev"

    from sourceSets.api.output
}

task remapApiJar(type: RemapJarTask, dependsOn: apiJar) {
    classifier "api"

    input = apiJar.archiveFile.get().asFile
    addNestedDependencies = false
}

build.dependsOn remapApiJar
*/

if (ENV.CURSEFORGE_KEY) {
    curseforge {
        apiKey = ENV.CURSEFORGE_KEY
        project {
            id = project.curseforge_id
            releaseType = project.curseforge_type
            addGameVersion "Forge"
            addGameVersion "1.16.4"
            addGameVersion "1.16.5"
            mainArtifact(remapJar.archivePath)
            relations {
            }
            changelog = ENV.CHANGELOG
            changelogType = 'markdown'
        }
    }
}

// Used to temporarily add mods to the workspace for debugging
def customGradle = rootProject.file('custom.gradle');
if (customGradle.exists()) {
    apply from: customGradle;
}
